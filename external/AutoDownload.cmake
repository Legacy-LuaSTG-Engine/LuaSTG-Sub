# download xaudio2.redist nuget package

set(_XAUDIO2_RESIST_NUPKG_URL  https://www.nuget.org/api/v2/package/Microsoft.XAudio2.Redist/1.2.9)
set(_XAUDIO2_RESIST_NUPKG      ${CMAKE_BINARY_DIR}/XAudio2Redist/microsoft.xaudio2.redist.1.2.9.nupkg)
set(_XAUDIO2_RESIST_NUPKG_HASH A02332CB8D4096C29430BE0FDB6A079E8F4A29781623AE362A811FD5DC015BB5)
set(_XAUDIO2_RESIST_DIR        ${CMAKE_SOURCE_DIR}/external/XAudio2Redist)

if(EXISTS ${_XAUDIO2_RESIST_NUPKG})
    file(SHA256 ${_XAUDIO2_RESIST_NUPKG} _CUR_XAUDIO2_RESIST_NUPKG_HASH)
    string(TOUPPER ${_CUR_XAUDIO2_RESIST_NUPKG_HASH} _CUR_XAUDIO2_RESIST_NUPKG_HASH)
    if(NOT (${_CUR_XAUDIO2_RESIST_NUPKG_HASH} STREQUAL ${_XAUDIO2_RESIST_NUPKG_HASH}))
        file(REMOVE ${_XAUDIO2_RESIST_NUPKG})
        message(WARNING "corrupt file:" ${_XAUDIO2_RESIST_NUPKG})
    endif()
endif()

if(NOT EXISTS ${_XAUDIO2_RESIST_NUPKG})
    file(DOWNLOAD
        ${_XAUDIO2_RESIST_NUPKG_URL}
        ${_XAUDIO2_RESIST_NUPKG}
        EXPECTED_HASH SHA256=${_XAUDIO2_RESIST_NUPKG_HASH}
        SHOW_PROGRESS
    )
endif()

if(NOT EXISTS ${_XAUDIO2_RESIST_DIR}/Microsoft.XAudio2.Redist.nuspec)
    file(ARCHIVE_EXTRACT
        INPUT ${_XAUDIO2_RESIST_NUPKG}
        DESTINATION ${_XAUDIO2_RESIST_DIR}
    )
endif()
